#!/usr/bin/env perl
#
# Author: petr.danecek@sanger
#

use strict;
use warnings;
use Carp;

my $runner = myRunner->new();
$runner->run();

exit;

#--------------------------------

package myRunner;
use base qw(Runner);
use strict;
use warnings;

sub new
{
    my ($class,@args) = @_;
    my $self = $class->SUPER::new(@args);

    $$self{config_version} = '1427807557';
    $$self{chunk_size} = 1_000_000;
    $$self{freebayes} = 'LD_LIBRARY_PATH=/software/hgi/pkglocal/gcc-4.9.1/lib:/software/hgi/pkglocal/gcc-4.9.1/lib64:$LD_LIBRARY_PATH /nfs/users/nfs_p/pd3/sandbox/svn/freebayes/freebayes/bin/freebayes';
    $$self{limits}    = { runtime=>24*60, memory=>5_000 };
    $$self{do_clean}  = 1;
    $$self{verbose}   = 1;

    $$self{_sampleconf} = q[
            # For sanity checking that your config file is not obsolete. Update the version key in your 
            #  config file to get rid of the warnings.
            version  => '] .$$self{config_version}. q[',

            chunk_size => '] .$$self{chunk_size}.  q[',
            freebayes  => '] .$$self{freebayes}.  q[',
            fasta      => undef,
            bam        => undef,

            limits     => { runtime=>24*60, memory=>10_000 },
            do_clean   => 1,
    ]."\n";


    $$self{usage} .= 
        "About: Run freebayes\n" .
        "Usage: run-freebayes\n" .
        "Options:\n" .
        "   -b, --bam <file>        \n" .
        "   -f, --fasta <file>      \n" .
        "   -o, --outdir <dir>      \n" .
        "   -t, --targets <file>    \n" .
        "\n";

    return $self;
}

sub parse_args
{
    my ($self) = @_;
    while (defined(my $arg=shift(@ARGV)))
    {
        if ( $arg eq '-b' or $arg eq '--bam' ) { $$self{bam}=shift(@ARGV); next; }
        if ( $arg eq '-f' or $arg eq '--fasta' ) { $$self{fasta}=shift(@ARGV); next; }
        if ( $arg eq '-o' or $arg eq '--outdir' ) { $$self{outdir}=shift(@ARGV); next; }
        if ( $arg eq '-t' or $arg eq '--targets' ) { $$self{targets}=shift(@ARGV); next; }
        $self->throw();
    }
    if ( !exists($$self{outdir}) ) { $self->throw("Expected the -o option."); }
    if ( !defined($$self{bam}) ) { $self->throw("Expected the -b option."); }
    if ( !defined($$self{fasta}) ) { $self->throw("Expected the -f option."); }
}

sub main
{
    my ($self) = @_;
    $self->parse_args();
    if ( $self->is_finished("$$self{outdir}/all_done") ) { $self->all_done; }

    my $chunks = $self->read_fai("$$self{fasta}.fai");
    if ( !@$chunks ) { $self->throw("No chunks to run?\n"); }

    $self->set_limits(%{$$self{limits}}) unless !defined($$self{limits});

    # chunks:
    #   - chr
    #   - beg
    #   - end
    #   + file .. full path
    for my $chunk (@$chunks)
    {
        my $outfile = "$$self{outdir}/chunks/$$chunk{chr}-$$chunk{beg}-$$chunk{end}.bcf";
        $$chunk{file} = $outfile;
        $self->spawn('run_freebayes',$outfile,$chunk);
    }
    $self->wait;

    $self->spawn('merge',"$$self{outdir}/merged.bcf",$chunks);
    $self->wait;

    $self->clean();
    $self->cmd("touch $$self{outdir}/all_done");
    $self->all_done;
}

sub clean
{
    my ($self,$chunks) = @_;
    if ( !$$self{do_clean} )  { return; }
    $self->SUPER::clean($$self{outdir});
    $self->cmd("rm -rf $$self{outdir}/chunks");
}

sub index_part
{
    my ($self,$vcf) = @_;
    $self->cmd("bcftools index -f $vcf.part");
    my $suffix = 'csi';
    rename("$vcf.part.$suffix","$vcf.$suffix") or $self->throw("rename $vcf.part.$suffix $vcf.$suffix: $!");
    rename("$vcf.part",$vcf) or $self->throw("rename $vcf.part $vcf: $!");
}

sub merge
{
    my ($self,$outfile,$chunks) = @_;
    open(my $fh,'>',"$outfile.list") or $self->throw();
    for my $chunk (@$chunks)
    {
        print $fh "$$chunk{file}\n";
    }
    close($fh) or $self->throw("close failed: $!");

    $self->cmd("bcftools concat -f $outfile.list -Ou | bcftools view -e'type=\"ref\"' -Ou | bcftools sort -Ou | bcftools norm -f $$self{fasta} -Ob -o $outfile.part");

    $self->index_part($outfile);
}

sub read_fai
{
    my ($self,$fai) = @_;

    my %chrs = ();
    if ( exists($$self{targets}) )
    {
        open(my $fh,'<',$$self{targets}) or $self->throw("$$self{targets}: $!");
        while (my $line=<$fh>)
        {
            chomp($line);
            $line =~ s/^\s*//;
            $line =~ s/\s*$//;
            $chrs{$line} = 1;
        }
        close($fh) or $self->throw("close failed: $$self{targets}");
    }

    # Determine the chromosomes and their lengths
    open(my $fh,'<',$fai) or $self->throw("$fai: $!"); 
    my @chunks = ();
    while (my $line=<$fh>)
    {
        my ($chr,$end,undef) = split(/\t/,$line);
        if ( scalar keys %chrs && !exists($chrs{$chr}) ) { next; }

        my $beg = 1;
        push @chunks, { chr=>$chr, beg=>$beg, end=>$end };
        if ( !$$self{chunk_size} or $chunks[-1]{end} - $chunks[-1]{beg} + 1 < $$self{chunk_size} ) { next; }

        # chunk the contigs into smaller pieces
        while ( $chunks[-1]{end} - $chunks[-1]{beg} + 1 > $$self{chunk_size} )
        {
            my $beg = $chunks[-1]{beg} + $$self{chunk_size};
            my $end = $chunks[-1]{end};
            $chunks[-1]{end} = $beg - 1;
            push @chunks, { chr=>$chr, beg=>$beg, end=>$end };
        }

        # if last chunk is too small, make the previous bigger
        if ( $chunks[-1]{end} - $chunks[-1]{beg} + 1 < 0.3*$$self{chunk_size} )
        {
            my $end = $chunks[-1]{end};
            pop @chunks;
            $chunks[-1]{end} = $end;
        }
    }
    close($fh);

    if ( !@chunks ) { $self->throw("No sequences read from $fai??"); }
    return \@chunks;
}

sub run_freebayes
{
    my ($self,$outfile,$chunk) = @_;
    my $cmd = "$$self{freebayes} --bam $$self{bam} --region $$chunk{chr}:$$chunk{beg}-$$chunk{end} -f $$self{fasta} | bcftools view -Ob -o $outfile.part";
    $self->cmd($cmd,verbose=>1);
    $self->index_part($outfile);
}

