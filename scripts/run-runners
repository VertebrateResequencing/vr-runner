#!/usr/bin/env perl
#
# Author: petr.danecek@sanger
#
# See run-runners --usage for a detailed help.
#

use strict;
use warnings;
use Carp;
use Cwd;
use IPC::Run3;

my $opts = parse_params();

create_lock($opts);
while (1)
{
    my $projects = list_projects($opts);
    for my $project (@$projects)
    {
        run_project($opts,$project);
    }
    if ( !$$opts{loop} ) { last; }
    print STDERR "sleeping $$opts{loop} seconds..\n" unless !$$opts{verbose};
    sleep($$opts{loop});
}
remove_lock($opts);

exit;

#--------------------------------

sub error
{
    my (@msg) = @_;
    if ( scalar @msg ) { confess @msg; }
    print 
        "About: Maintain a dropbox-style runner chains.\n",
        "Usage: run-runners [OPTIONS]\n",
        "Options:\n",
        "   -d, --dropbox <prefix>  prefix to dropbox directory (read \"run-runners --usage\" first!)\n",
        "   -i, --ignore-failures   continue with other projects even if one keeps failing\n",
        "   -l, --loop <int>        run in daemon mode, check the status every <int> seconds\n",
        "   -L, --lock <file>       exit if another instance is already running\n",
        "   -n, --nmax <int>        maximum number of projects to run simultaneously\n",
        "   -u, --usage             print detailed usage explanation\n",
        "   -v, --verbose           increase verbosity by giving multiple times\n",
        "   -h, -?, --help          this help message\n",
        "Examples:\n",
        "   run-runners --usage | less          # detailed usage explanation\n",
        "   run-runners -v -d prefix -l 300     # loop with sleep cycles of 300 seconds\n",
        "\n";
    exit -1;
}

sub parse_params
{
    my $opts = { nmax_projects=>1 };
    while (defined(my $arg=shift(@ARGV)))
    {
        if ( $arg eq '-u' || $arg eq '--usage' ) { detailed_usage(); exit; }
        if ( $arg eq '-d' || $arg eq '--dropbox' ) { $$opts{dropbox}=shift(@ARGV); next }
        if ( $arg eq '-i' || $arg eq '--ignore-failures' ) { $$opts{ignore_failures}=1; next }
        if ( $arg eq '-n' || $arg eq '--nmax' ) { $$opts{nmax_projects}=shift(@ARGV); next }
        if ( $arg eq '-L' || $arg eq '--lock' ) { $$opts{lock}=shift(@ARGV); next }
        if ( $arg eq '-l' || $arg eq '--loop' ) { $$opts{loop}=shift(@ARGV); next }
        if ( $arg eq '-v' || $arg eq '--verbose' ) { $$opts{verbose}++; next }
        if ( $arg eq '-?' || $arg eq '-h' || $arg eq '--help' ) { error(); }
        error("Unknown parameter \"$arg\". Run -h for help.\n");
    }
    if ( !exists($$opts{dropbox}) ) { error("Missing the --dropbox option.\n"); }

    $$opts{dropbox_tmp}  = "$$opts{dropbox}.tmp";
    $$opts{dropbox_in}   = "$$opts{dropbox}.in";
    $$opts{dropbox_out}  = "$$opts{dropbox}.out";
    $$opts{dropbox_conf} = "$$opts{dropbox}.conf";

    if ( !-d $$opts{dropbox_conf} ) { error("Incorrect dropbox prefix, the directory not found: $$opts{dropbox_conf}\n\n"); }
    if ( !-e $$opts{dropbox_in} ) { `mkdir -p $$opts{dropbox_in}`; }
    if ( !-e $$opts{dropbox_out} ) { `mkdir -p $$opts{dropbox_out}`; }
    if ( !-e $$opts{dropbox_tmp} ) { `mkdir -p $$opts{dropbox_tmp}`; }

    return $opts;
}

sub detailed_usage
{
    print << "EOT";

 Runner pipelines
 ----------------

 This lightweight pipeline framework is intended for chaining multiple runners.
 It runs as a daemon and waits for new projects to appear in a dropbox
 directory. After completion, the results appear in output directory and the
 user is notified by an email. Multiple users can use the same dropbox to run
 different pipelines at the same time.
 
 The following directory structure is assumed:
 
   prefix.conf .. config directory which contains pipeline definitions. 
                   Typically this is a symbolic link to the vr-runner/misc
                   install directory.  This is the only requirement, the rest
                   is created automatically by the pipeline
   prefix.in   .. dropbox input directory, drop your project here
   prefix.out  .. output directory, the result will appear here
   prefix.tmp  .. working directory
 
 
 Each pipeline chain consists of a series of "steps" defined in the misc/chain.*.conf
 and misc/step.*.conf files. These configs can be used as they are. If well
 written, the user only needs to create a small project description and place it
 in the dropbox input directory:

    config:          chain.gtcheck.conf
    email:           someone\@somewhere.org
    mpileup/alns:    bams.list
    mpileup/fa_ref:  human_g1k_v37.fasta


 The project descriptions are small text files which define the input data:
 
   # The pipeline config, known also as the "chain file". Note that in order to
   # avoid the execution of an arbitrary code passed by a malicious user, the config
   # must be a file name with any leading directory components removed. The file
   # must exist in the dropbox config directory:
   config: chain.pipeline.conf
 
   # The rest is optional and pipeline-specific. There are a few pipeline-wide 
   # options that are recognised by all pipelines, such as where to send email 
   # notifications about job completion and failures:
   email: someone\@somewhere.org
 
   # Frequency (in seconds) with which to remind about failed jobs, so that
   # the mailbox does not end up cluttered by emails. If not given, the default
   # of 3600 seconds is used:
   err_period: 3600
 
   # Any key in a runner's config file can be overriden. For example, if a pipeline
   # chain includes a runner step named "step_name" which recognises the config 
   # key "var_name", the default key can be overriden as:
   step_name/var_name: new value
 
   # Similarly, substrings in a runner's config file can be expanded. For example,
   # if the config contains the following key-value pair
   #   key => 'some \$(value)',
   # the variable "\$(value)" can be replaced with "new value":
   step_name/value: new value
 
   # Note: if the project description does not specify the "step_name/value" key,
   # the variable "\$(value)" will be replaced with "value".
 
   # In order to preserve compactness of project descriptions yet allowing flexibility,
   # the runner's config files can use default values. For example, if the config
   # contains the following key-value pair
   #   key => 'some \$(value:sensible default)',
   # the project description file can override the value but in case it does not,
   # the key will be expanded as follows
   #   key => 'some sensible default',
   #
   # See the misc/*conf files for real-life examples.
 
 
 The pipeline chain can be run from the command line:
 
   run-runners -v -d prefix            # one iteration
   run-runners -v -d prefix -l 300     # loop with sleep cycles of 300 seconds
 
 or from cron:
 
   */5 *  *   *   *     vr-wrapper ~/.vrw/runners 'run-runners -v -d prefix -L config.lock'    
 
 The vr-wrapper scripts can be used to set environment variables without having
 to change user's profile. It may look as this:
 
   #!/bin/bash
   export PATH="\$HOME/git/vr-runner/scripts:\$PATH"
   export PERL5LIB="\$HOME/git/vr-runner/modules:\$PERL5LIB"
   
EOT
}


sub create_lock
{
    my ($opts) = @_;

    if ( !exists($$opts{lock}) ) { return; }

    my $lock = $$opts{lock};
    if ( -e $lock )
    {
        # Find out the PID of the running pipeline
        open(my $fh,'<',$lock) or error("$lock; $!");
        while (my $pid=<$fh>)
        {
            chomp($pid);
            if ( !($pid=~/^\d+$/) ) { die "Could not parse lock file: $lock, $pid\n"; }

            # Is the process still running?
            my $running = kill 0, $pid;
            if ( $running )
            {
                my @out = `ps --no-headers -p $pid -o command`;
                my $script_name = $0;
                $script_name =~ s{^.*/}{};
                if ( $out[0]=~/$script_name/ )
                {
                    die "Another process is running ($pid), exiting.\n";
                }
            }

            print STDERR "Ignoring an old lock file, PID $pid is not running.\n";
            last;
        }
        close($fh);
    }

    open(my $fh,'>',$lock) or error("$lock: $!");
    print $fh $$ . "\n";
    close($fh);
}

sub remove_lock
{
    my ($opts) = @_;
    if ( !exists($$opts{lock}) ) { return; }
    my $lock = $$opts{lock};
    if ( -e $lock ) { unlink($lock); }
}

sub list_projects
{
    my ($opts) = @_;
    my @projects = ();

    # First check projects in progress. Each project had been assigned a unique
    # name: <numeric timestamp>_<user name>
    #
    my ($dh,$project);
    opendir($dh, $$opts{dropbox_tmp}) or error("$$opts{dropbox_tmp}: $!");
    while (my $dir = readdir($dh))
    {
        if ( !-d "$$opts{dropbox_tmp}/$dir" or !($dir=~/^\d+_(.*)$/ ) ) { next; }
        $project = parse_project($opts,"$$opts{dropbox_tmp}/$dir/$1");
        if ( !defined $project ) { next; }
        push @projects, $project;
        if ( scalar @projects >= $$opts{nmax_projects} ) { last; }
    }
    closedir($dh);
    if ( scalar @projects >= $$opts{nmax_projects} ) { return \@projects; }


    # No unfinished projects, check for new projects. File names starting
    # with a dot, ending with ~, or .err are ignored. Everything else is
    # parsed as an input.
    #
    opendir($dh, $$opts{dropbox_in}) or error("$$opts{dropbox_in}: $!");
    while (my $file = readdir($dh))
    {
        if ( $file =~/^\./ ) { next; }
        if ( $file =~/~$/ ) { next; }
        if ( $file =~/\.err$/ ) { next; }
        $project = parse_project($opts,"$$opts{dropbox_in}/$file");
        if ( !defined $project ) { next; }
        $$project{alias} = time() .'_'. $$project{name};
        $$project{file}  = $file;

        system("mkdir -p $$opts{dropbox_tmp}/$$project{alias}");
        system("mv $$opts{dropbox_in}/$$project{file} $$opts{dropbox_tmp}/$$project{alias}/$$project{file}");
        if ( $? ) { confess "mv $$opts{dropbox_in}/$$project{file} $$opts{dropbox_tmp}/$$project{alias}/$$project{file}"; }

        push @projects, $project;
        if ( scalar @projects >= $$opts{nmax_projects} ) { last; }
    }
    closedir($dh);
    return \@projects;
}

sub clean_project
{
    my ($opts,$project) = @_;

    my $name = $$project{name};
    debug_msg($opts,"Cleaning project: $name\n");
    $$project{prefix} = "$$opts{dropbox_tmp}/$$project{alias}/$name";

    for my $outdir (keys %{$$project{outputs}})
    {
        if ( ref($$project{outputs}{$outdir}) eq 'ARRAY' )
        {
            # create a subdirectory '$outdir' and copy all listed files in there
            cmd($opts,"mkdir -p $$opts{dropbox_out}/$$project{alias}/$outdir");
            for my $file (@{$$project{outputs}{$outdir}})
            {
                my $path = expand_vars($opts,$project,$file);
                cmd($opts,"mv $path $$opts{dropbox_out}/$$project{alias}/$outdir/");
            }
        }
        else
        {
            cmd($opts,"mkdir -p $$opts{dropbox_out}/$$project{alias}");
            my $path = expand_vars($opts,$project,$outdir);
            cmd($opts,"mv $path $$opts{dropbox_out}/$$project{alias}/");
        }
    }

    cmd($opts,"mkdir -p $$opts{dropbox_out}/$$project{alias}/tmp-data");
    cmd($opts,"mv $$opts{dropbox_tmp}/$$project{alias} $$opts{dropbox_out}/$$project{alias}/tmp-data");
}

# Parse the project definition
sub parse_project
{
    my ($opts,$file) = @_;
    if ( !($file=~m{^(.+)/([^/]+)$}) ) { error("Could not parse dir/fname: $file\n"); }
    my $dir  = $1;
    my $name = $2;
    my %project = ();
    my @override = ();
    open(my $fh,'<',$file) or error("$file: $!");
    while (my $line=<$fh>)
    {
        if ( $line=~/^\s*#/ ) { next; }
        if ( $line=~/^\s*$/ ) { next; }
        $line =~ s/^\s*//;
        $line =~ s/\s*$//;
        if ( !($line=~/^\s*([^:]+)\s*:/) ) 
        { 
            print STDERR "Could not parse $file: $line\n";
            if ( $$opts{ignore_failures} ) { return undef; }
            error("Failed: $file\n");
        }
        my $key = $1;
        my $value = $';
        $value =~ s/^\s*//;
        $value =~ s/\s*$//;
        $value = parse_config_value($value);    # expand deep variables (arrays or hashes)
        $key   =~ s/\s*$//;
        if ( $key =~ m{/} )
        {
            push @override, [$key,$value];      # user variable can override a default config key
            next;
        }
        $project{$key} = $value;
    }
    close($fh) or error("close failed: $file");
    $project{dir}  = $dir;
    $project{name} = $name;
    if ( $dir=~m{^.+/(\d+_[^/]+)$} ) { $project{alias} = $1; }

    # Parse project's config file
    if ( !exists($project{config}) )
    {
        print STDERR "Config not present in $file\n";
        if ( $$opts{ignore_failures} ) { return undef; }
        error("Failed: $file\n");
    }
    if ( $project{config} =~ m{/} )
    {
        print STDERR "The config must be a plain file in $$opts{dropbox_conf},\n";
        print STDERR "please remove the directory part from $project{config}.\n";
        if ( $$opts{ignore_failures} ) { return undef; }
        error("Failed: $file\n");
     }
    my $config_fname = "$$opts{dropbox_conf}/$project{config}";
    my $ret = open($fh,'<',$config_fname);
    if ( !$ret ) 
    {
        print STDERR "No such config in $$opts{dropbox_conf}:\n\t$config_fname\n";
        if ( $$opts{ignore_failures} ) { return undef; }
        error("Failed: $file\n");
    }
    my @config_lines = <$fh>;
    close($fh) or error("close failed: $config_fname");
    my $config_str = join('',@config_lines);
    my $x = eval "{ $config_str }";
    if ( $@ ) { error("eval $config_fname: $@\n"); }

    $x = parse_config($opts,$config_fname,\@override,$x);
    %project = (%project, %$x);
    if ( !@{$project{steps}} ) { error("No steps defined in the pipeline: $config_fname\n\n"); }

    return \%project;
}

sub parse_config_value
{
    my ($value) = @_;
    if ( $value=~/^\[/ && $value=~/\]$/ or $value=~/^\{/ && $value=~/\}$/ )
    {
        my $val = eval "$value";
        if ( !$@ ) { $value = $val; }
    }
    return $value;
}

sub parse_config
{
    my ($opts,$fname,$override,$config) = @_;
    for my $step (@{$$config{steps}})
    {
        if ( !exists($$config{$step}) ) { error("Step \"$step\" undefined in $fname\n"); }

        for my $key (keys %{$$config{$step}})
        {
            if ( $key ne 'include' ) { push @$override, ["$step/$key",$$config{$step}{$key}]; next; }

            my $config_fname = "$$opts{dropbox_conf}/$$config{$step}{include}";
            open(my $fh,'<',$config_fname) or error("$config_fname: $!");
            my @config_lines = <$fh>;
            close($fh) or error("close failed: $config_fname");
            my $config_str = join('',@config_lines);
            my $x = eval "{ $config_str }";
            if ( $@ ) { error("eval $config_fname: $@\n"); }
            $$config{$step} = {(%{$$config{$step}},%$x)};
        }
    }
    for my $var (@$override)
    {
        my ($step,$key) = split(m{/},$$var[0]);
        my $value = $$var[1];
        if ( exists($$config{$step}{config}) )
        {
            # The step has a config (i.e. a runner). In this case we create
            # a new config key but also expand all \$($key) occurances.
            $$config{$step}{config}{$key} = $value;
        }
        # All \$($key) occurances will be expanded
        $$config{$step}{$key} = $value;
    }

    # Convert config to text
    for my $step (@{$$config{steps}})
    {
        if ( exists($$config{$step}{config}) )
        {
            use Data::Dumper;
            my @config_lines = split(/\n/,Dumper($$config{$step}{config}));
            shift(@config_lines);       # get rid of $VAR1 = { .. } lines
            pop(@config_lines);
            $$config{$step}{config} = join("\n",@config_lines);
        }
    }
    return $config;
}

# Replace variables $(var_name)
sub expand_vars
{
    my ($opts,$project,$str) = @_;
    pos($str) = 0;
    while ( ($str =~ /\G(.*?)\$\(([^\)]+):([^\)]+)\)/s) )
    {
        pos($str) += length($&);
        if ( exists($$project{$2}) ) 
        { 
            $str = $` . $1 . $$project{$2} . $'; 
            next; 
        }
        else
        {
            $str = $` . $1 . $3 . $';       # replace "$(string:200)" with "200" if key "string" is not present
        }
    }
    pos($str) = 0;
    while ( ($str =~ /\G(.*?)\$\(([^\)]+)\)/s) )
    {
        pos($str) += length($&);
        if ( exists($$project{$2}) ) 
        { 
            $str = $` . $1 . $$project{$2} . $'; 
            next; 
        }
        else
        {
            $str = $` . $1 . $2 . $';       # replace "$(string)" with "string" if key "string" is not present
            next;
        }
    }
    return $str;
}

sub first_file_newer
{
    my ($a,$b) = @_;
    my $atime = (stat($a))[9];
    my $btime = (stat($b))[9];
    return $atime > $btime ? 1 : 0;
}

sub debug_msg
{
    my ($opts,@msg) = @_;
    if ( !$$opts{verbose} ) { return; }
    print STDERR join('',@msg);
}

# Create a config file for each step in project (if the key "config" exists for
# the step) and run all runners until finished.
sub run_project
{
    my ($opts,$project) = @_;
    my $name = $$project{name};
    debug_msg($opts,"Checking status: $name\n");
    for my $step (@{$$project{steps}})
    {
        my $prefix = "$$opts{dropbox_tmp}/$$project{alias}/$name";
        debug_msg($opts, "Checking step: $prefix.$step.done\n");
        if ( -e "$prefix.$step.done" ) 
        { 
            debug_msg($opts, "step done: $prefix.$step\n");
            next; 
        }
        if ( ! -e $prefix ) { system("mkdir -p $prefix.$step"); }
        $$project{prefix} = $prefix;
        $$project{step}   = $step;

        if ( exists($$project{$step}{config}) && !-e "$prefix.$step.conf" )
        {
            my $config = expand_vars($opts,$$project{$step},$$project{$step}{config});
            open(my $fh,'>',"$prefix.$step.conf.part") or error("$prefix.$step.conf.part: $!");
            print $fh $config;
            close($fh) or error("close failed: $prefix.$step.conf.part");
            rename("$prefix.$step.conf.part","$prefix.$step.conf") or error("rename $prefix.$step.conf.part $prefix.$step.conf: $!");
        }

        my $cwd;
        if ( $$project{chdir_into_project} )
        {
            print STDERR "chdir $$opts{dropbox_tmp}/$$project{alias}\n";
            $cwd = getcwd;
            chdir("$$opts{dropbox_tmp}/$$project{alias}");
            $$project{prefix} = $name;
        }
        my $ret = run_step($opts,$project,$step);
        if ( defined $cwd ) { chdir($cwd); }
        if ( !$ret ) { return; }

        system("touch $prefix.$step.done");
        if ( $? ) { confess "touch $prefix.$step.done"; }
    }
    clean_project($opts,$project);
    send_email($project,"The project \"$name\" finished","The project \"$name\" finished:\n\t$$opts{dropbox_out}/$$project{alias}");
}

sub cmd
{
    my ($opts,$cmd) = @_;

    my $tmp = $$opts{lock} ? $$opts{lock} : "/tmp/runners.$$";
    open(my $stderr,'>',"$tmp.e") or confess("$tmp.e: $!");
    open(my $stdout,'>',"$tmp.o") or confess("$tmp.o: $!");

    my @cmd = ('/bin/bash', '-o','pipefail','-c', $cmd);
    run3 \@cmd,undef,$stdout,$stderr;

    close($stderr);
    close($stdout);

    my $signal  = $? & 127;
    my $status  = $? >> 8;

    my (@out,@err);
    if ( open(my $fh,'<',"$tmp.o") )
    {
        @out = <$fh>;
        close($fh);
    }
    if ( open(my $fh,'<',"$tmp.e") )
    {
        @err = <$fh>;
        close($fh);
    }
    unlink("$tmp.o");
    unlink("$tmp.e");

    return ($signal,$status,\@out,\@err);
}

# Run a single runner. There can be multiple steps per pipeline (project).
sub run_step
{
    my ($opts,$project,$step_name) = @_;
    my $step = $$project{$step_name};
    $$step{prefix} = $$project{prefix};
    $$step{step}   = $step_name;
    for my $job (@{$$step{run}})
    {
        my $cmd = expand_vars($opts,$step,$$job{cmd});

        if ( !exists($$job{status}) or !exists($$job{status}{done}) ) { $$job{status}{done} = [0]; }
        my %done = map { $_=>1 } @{$$job{status}{done}};
        my %loop = exists($$job{status}{loop}) ? map { $_=>1 } @{$$job{status}{loop}} : ();

        while ( 1 )
        {
            debug_msg($opts, "Running step $step_name: $cmd\n");
            my ($signal,$status,$out,$err) = cmd($opts,$cmd);
            if ( !$signal && $done{$status} ) { last; }
            if ( !$signal && $loop{$status} )
            {
                if ( !$$opts{loop} ) { return 0; }
                print STDERR "sleeping $$opts{loop} seconds..\n" unless !$$opts{verbose};
                sleep($$opts{loop}); 
            }
            else 
            { 
                my $out = @$out ? join('',@$out) : '<none>';
                my $err = @$err ? join('',@$err) : '<none>';
                my $msg = 
                        "\n---\nError: unexpected signal/status ($signal/$status) from the command:\n" .
                        "\t$cmd\n\n".
                        "Working directory:\n\t$$opts{dropbox_tmp}/$$project{alias}/\n\n".
                        "Standard output:\n$out\n\n".
                        "Error output:\n$err\n---\n\n";

                send_err_email($project,"$step_name error",$msg);

                if ( $$opts{ignore_failures} )
                {
                    print STDERR "Failed: $cmd\n\n";
                    return 0;   # Act as if the step has not finished
                }
                error($msg); 
            }
        }
    }
    return 1;
}

sub send_email
{
    my ($opts, $status, @msg) = @_;
    if ( !exists($$opts{email}) ) { return; }
    open(my $mh,"| mail -s 'Runner report: $status' $$opts{email}");
    print $mh join('',@msg) . "\n";
    close($mh);
}
sub send_err_email
{
    my ($opts, $status, @msg) = @_;
    if ( !exists($$opts{email}) ) { return; }
    if ( exists($$opts{err_period}) )
    {
        my $err_file = "$$opts{dir}/$$opts{prefix}.err";
        if ( -e $err_file ) 
        {
            if ( !update_after_delta($err_file,$$opts{err_period}) ) { return; }
        }
        open(my $fh,'>',$err_file) or error("$err_file: $!");
        my $now = time();
        print $fh $now;
        close($fh) or error("$err_file: close failed");
    }
    send_email($opts, $status, @msg);
}

sub update_after_delta
{
    my ($err_file,$period) = @_;
    my $now = time();
    my $ret = open(my $fh,'<',$err_file);
    if ( $ret )
    {
        my $then = <$fh>;
        close($fh);
        if ( $then )
        {
            chomp($then);
            if ( $then + $period > $now ) { return 0; }
        }
    }
    open($fh,'>',$err_file) or return 1;
    print $fh $now;
    close($fh);
    return 1;
}


