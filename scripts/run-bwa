#!/usr/bin/env perl
#
# Author: petr.danecek@sanger
#

use strict;
use warnings;
use Carp;

my $runner = myRunner->new();
$runner->run();

exit;

#--------------------------------

package myRunner;
use base qw(Runner);
use strict;
use warnings;

sub new
{
    my ($class,@args) = @_;
    my $self = $class->SUPER::new(@args);

    $$self{verbose} = 1;
    $$self{reads_per_chunk} = 100_000;
    $$self{samtools} = 'samtools';
    $$self{bwa}      = 'bwa';
    $$self{picard}   = 'java -Xms5G -Xmx5G -jar ~/lib/picard-1.135.jar MarkDuplicates VALIDATION_STRINGENCY=SILENT';
    $$self{bwa_mem}  = 1;
    $$self{merge}    = 0;
    $$self{rmdup}    = 0;
    $$self{aln_args} = '';
    $$self{limits}   = { memory=>3_500 };
    $$self{config_version} = '1427807557';
    $$self{_sampleconf} = q[
            # For sanity checking that your config file is not obsolete. Update
            # the version key in your config file to get rid of the warnings.
            version  => '] .$$self{config_version}. q[',

            # List of fastq files, one file per line followed by output
            # file name and RG line, white-space separated:
            #   file.fastq  out.bam  @RG\tID:lane1\tSM:60A_Sc_DBVPG6044
            # If the second field is "-", autogenerate the name.
            fastq_se  => undef,

            # Same as fastq_se, but comma-separated pairs of input files:
            #   file1.fastq,file2.fastq  out.bam  @RG\tID:lane1\tSM:60A_Sc_DBVPG6044
            # If the second field is "-", autogenerate the name.
            fastq_pe  => undef,

            # List of unmapped bams with read pairs sorted by name
            bam_pe    => undef,

            # Sort if the bams are not sorted
            # bam_sort  => 'samtools view -F 0x100 -bu {IN} | samtools collate -n 128 - {OUT}',

            # If defined, use `bwa mem -C` to transfer an existing bam tag
            # bam_tag => 'BX',

            # reference sequence in fasta format
            fa_ref => undef,

            # executables
            bwa      => '] .$$self{bwa}. q[',
            samtools => '] .$$self{samtools}. q[',
            picard   => '] .$$self{picard}. q[',

            bwa_mem  => 1,
            aln_args => '] .$$self{aln_args}. q[',

            # optional filter to stream through, such as `samtools view -q25 -bu`
            filter => '',

            # merge bams into one
            merge  => 0,

            # run duplicate removal (picard)
            rmdup  => 0,

            # split fastq's into smaller chunks to run in parallel
            reads_per_chunk => ] .$$self{reads_per_chunk}. q[,

            limits => { memory=>3_500 },

    ]."\n";

    $$self{usage} .= 
        "About: Align fastq files via bwa\n" .
        "Usage: run-bwa\n" .
        "Options:\n" .
        "   -a,  --aln-args <string>        Arguments to pass to bwa\n" .
        "   -b2, --bam-pe-list <file>       File with paired-end bam or cram files\n" .
        "   -f1, --fastq-se-list <file>     File with single-end fastq files\n" .
        "   -f2, --fastq-pe-list <file>     File with paired-end fastq files\n" .
        "   -o,  --outdir <dir>             Output directory\n" .
        "   -O,  --options <list>           Options, eg. merge:1,rmdup:1\n" .
        "   -r,  --ref-fa <file>            Reference sequence in fasta format\n" .
        "\n";

    return $self;
}

sub parse_args
{
    my ($self) = @_;
    while (defined(my $arg=shift(@ARGV)))
    {
        if ( $arg eq '-O' or $arg eq '--options' )
        {
            my $opts = shift(@ARGV);
            my @opts = split(/,/,$opts);
            for my $opt (@opts)
            {
                my ($key,$val) = split(/:/,$opt);
                $$self{$key} = $val;
            }
            next; 
        }
        if ( $arg eq '-a' or $arg eq '--aln-args' ) { $$self{aln_args}=shift(@ARGV); next; }
        if ( $arg eq '-b2' or $arg eq '--bam-pe-list' ) { $$self{bam_pe}=shift(@ARGV); next; }
        if ( $arg eq '-f1' or $arg eq '--fastq-se-list' ) { $$self{fastq_se}=shift(@ARGV); next; }
        if ( $arg eq '-f2' or $arg eq '--fastq-pe-list' ) { $$self{fastq_pe}=shift(@ARGV); next; }
        if ( $arg eq '-r' or $arg eq '--ref' ) { $$self{fa_ref}=shift(@ARGV); next; }
        if ( $arg eq '-o' or $arg eq '--outdir' ) { $$self{outdir}=shift(@ARGV); next; }
        if ( $arg eq '-?' or $arg eq '-h' or $arg eq '--help' ) { $self->throw(); }
        $self->throw("Unknown argument: $arg\n");
    }
    if ( !defined($$self{outdir}) ) { $self->throw("Expected the -o option."); }
    if ( !defined($$self{fastq_pe}) && !defined($$self{fastq_se}) && !defined($$self{bam_pe}) ) { $self->throw("Expected the -f1, -f2 or -b2 option."); }
    if ( !defined($$self{fa_ref}) ) { $self->throw("Expected the -r option."); }
    if ( exists($$self{bam_tag}) && !$$self{bwa_mem} ) { $self->throw("Need bwa_mem with bam_tag\n"); }
}

sub main
{
    my ($self) = @_;
    $self->parse_args();
    `mkdir -p $$self{outdir}`;
    $self->save_config('bwa');

    if ( $self->is_finished("$$self{outdir}/all_done") ) { $self->all_done; }

    # split fastqs
    my @files = $self->list_se_files($$self{fastq_se});
    @files = (@files,$self->list_pe_files($$self{fastq_pe}),$self->list_bam_files($$self{bam_pe}));

    for my $file (@files)
    {
        if ( 0 && $$self{bwa_mem} )
        {
            $self->set_limits(%{$$self{limits}}) unless (!exists($$self{limits}) or !exists($$self{bam_sort}));
            $self->spawn('split_bam2fq',"$$self{outdir}/chunks/$$file{prefix}.chunks",$$file{bam});
        }
        elsif ( $$file{se} )
        {
            $self->spawn('split_fastq',"$$self{outdir}/chunks/$$file{prefix}.chunks",$$file{fastq});
        }
        elsif ( $$file{pe} )
        {
            $self->spawn('split_fastq',"$$self{outdir}/chunks/$$file{prefix}.chunks1",$$file{fastq1});
            $self->spawn('split_fastq',"$$self{outdir}/chunks/$$file{prefix}.chunks2",$$file{fastq2});
        }
        elsif ( $$file{bam_pe} )
        {
            $self->set_limits(%{$$self{limits}}) unless (!exists($$self{limits}) or !exists($$self{bam_sort}));
            $self->spawn('split_bam',"$$self{outdir}/chunks/$$file{prefix}.chunks",$$file{bam});
        }
    }
    $self->wait;

    # index fastq files
    $self->set_limits(%{$$self{limits}}) unless !exists($$self{limits});
    for my $file (@files)
    {
        if ( $$self{bwa_mem} )
        {
            # using bwa mem, no indexing step
        }
        elsif ( $$file{se} )
        {
            $$file{chunks} = $self->list_chunks("$$self{outdir}/chunks/$$file{prefix}.chunks");
            for my $chunk (@{$$file{chunks}}) { $self->spawn('bwa_aln',"$chunk.sai",$chunk,$$self{aln_args}); }
        }
        elsif ( $$file{pe} )
        {
            $$file{chunks1} = $self->list_chunks("$$self{outdir}/chunks/$$file{prefix}.chunks1");
            $$file{chunks2} = $self->list_chunks("$$self{outdir}/chunks/$$file{prefix}.chunks2");
            if ( scalar @{$$file{chunks1}} != scalar @{$$file{chunks2}} )
            {
                $self->throw("Different number of chunks?! $$self{outdir}/chunks/$$file{prefix}.chunks*"); 
            }
            for my $chunk (@{$$file{chunks1}}) { $self->spawn('bwa_aln',"$chunk.sai1",$chunk,$$self{aln_args}); }
            for my $chunk (@{$$file{chunks2}}) { $self->spawn('bwa_aln',"$chunk.sai2",$chunk,$$self{aln_args}); }
        }
        elsif ( $$file{bam_pe} )
        {
            $$file{chunks} = $self->list_chunks("$$self{outdir}/chunks/$$file{prefix}.chunks");
            for my $chunk (@{$$file{chunks}}) { $self->spawn('bwa_aln_bam',"$chunk.sai1","$chunk.sai2",$chunk,$$self{aln_args}); }
        }
    }
    $self->wait;

    # align
    for my $file (@files)
    {
        if ( $$self{bwa_mem} )
        {
            if ( $$file{pe} )
            {
                $$file{chunks1} = $self->list_chunks("$$self{outdir}/chunks/$$file{prefix}.chunks1");
                $$file{chunks2} = $self->list_chunks("$$self{outdir}/chunks/$$file{prefix}.chunks2");
            }
            else
            {
                my $chunks = $self->list_chunks("$$self{outdir}/chunks/$$file{prefix}.chunks");
                for my $chunk (@$chunks)
                {
                    my ($chunk1,$chunk2) = split(/\t/,$chunk);
                    push @{$$file{chunks1}},$chunk1;
                    push @{$$file{chunks2}},$chunk2;
                }
            }
            for (my $i=0; $i<@{$$file{chunks1}}; $i++)
            {
                my $chunk1 = $$file{chunks1}[$i];
                my $chunk2 = $$file{chunks2}[$i];
                $self->spawn('bwa_mem',"$chunk1.bam",fastqs=>"$chunk1 $chunk2",rg=>$$file{rg_line});
            }
        }
        elsif ( $$file{se} )
        {
            $$file{chunks} = $self->list_chunks("$$self{outdir}/chunks/$$file{prefix}.chunks");
            for my $chunk (@{$$file{chunks}})
            {
                $self->spawn('bwa_samse',"$chunk.bam",fastq=>$chunk,sai=>"$chunk.sai",rg=>$$file{rg_line}); 
            }
        }
        elsif ( $$file{pe} )
        {
            $$file{chunks1} = $self->list_chunks("$$self{outdir}/chunks/$$file{prefix}.chunks1");
            $$file{chunks2} = $self->list_chunks("$$self{outdir}/chunks/$$file{prefix}.chunks2");
            for (my $i=0; $i<@{$$file{chunks1}}; $i++)
            {
                my $chunk1 = $$file{chunks1}[$i];
                my $chunk2 = $$file{chunks2}[$i];
                $self->spawn('bwa_sampe',"$chunk1.bam",fastq1=>$chunk1,fastq2=>$chunk2,sai1=>"$chunk1.sai1",sai2=>"$chunk2.sai2",rg=>$$file{rg_line}); 
            }
        }
        elsif ( $$file{bam_pe} )
        {
            $$file{chunks} = $self->list_chunks("$$self{outdir}/chunks/$$file{prefix}.chunks");
            for my $chunk (@{$$file{chunks}})
            {
                $self->throw("Is this correct for bwa sampe: fastq1=fastq2=$chunk??!\n");
                $self->spawn('bwa_sampe',"$chunk.bam",fastq1=>$chunk,fastq2=>$chunk,sai1=>"$chunk.sai1",sai2=>"$chunk.sai2",rg=>$$file{rg_line}); 
            }
        }
    }
    $self->wait;

    # merge
    for my $file (@files)
    {
        if ( $$self{bwa_mem} )
        {
            $self->spawn('merge',"$$self{outdir}/bams/$$file{aln}",$$file{chunks1});
        }
        elsif ( $$file{se} )
        {
            $self->spawn('merge',"$$self{outdir}/bams/$$file{aln}",$$file{chunks});
        }
        elsif ( $$file{pe} )
        {
            $self->spawn('merge',"$$self{outdir}/bams/$$file{aln}",$$file{chunks1});
        }
        elsif ( $$file{bam_pe} )
        {
            $self->spawn('merge',"$$self{outdir}/bams/$$file{aln}",$$file{chunks});
        }
    }
    $self->wait;

    if ( $$self{merge} )
    {
        my @bams = ();
        for my $file (@files) { push @bams,"$$self{outdir}/bams/$$file{prefix}"; }
        $self->spawn('merge',"$$self{outdir}/merged.bam",\@bams);
    }
    $self->wait;

    if ( $$self{rmdup} )
    {
        my $mem = $self->get_java_limits($$self{picard});
        if ( defined $mem ) { $self->set_limits(memory=>$mem*1.15); }

        $self->spawn('rmdup',"$$self{outdir}/merged.rmdup.bam","$$self{outdir}/merged.bam");
        $self->wait;
    }

    # clean
    $self->clean(@files);

    $self->cmd("touch $$self{outdir}/all_done");
    $self->all_done;
}

sub save_config
{
    my ($self,$name) = @_;
    if ( !defined $$self{_config} ) { return; }
    my $src = $$self{_config};
    my $dst = "$$self{outdir}/$name.conf";
    if ( -e $dst && (stat($src))[9] <= (stat($dst))[9] ) { return; }
    if ( !-d $$self{outdir} ) { $self->cmd("mkdir -p $$self{outdir}"); }
    open(my $fh,'>',$dst) or $self->throw("$dst: $!");
    my $about = $$self{_about};
    $about =~ s/\n/\n# /g;
    print $fh "# $about";
    close($fh);
    $self->cmd("cat $src >> $dst");
}

sub clean
{
    my ($self,@files) = @_;
    for my $file (@files)
    {
        if ( $$self{bwa_mem} )
        {
            for my $chunk (@{$$file{chunks1}})
            {
                unlink("$chunk");
                unlink("$chunk.bam");
                unlink("$chunk.bam.bai");
            }
            for my $chunk (@{$$file{chunks2}})
            {
                unlink("$chunk");
            }
            unlink("$$self{outdir}/$$file{prefix}.chunks");
        }
        elsif ( $$file{se} )
        {
            for my $chunk (@{$$file{chunks}})
            {
                unlink("$chunk");
                unlink("$chunk.sai");
                unlink("$chunk.bam");
                unlink("$chunk.bam.bai");
            }
            unlink("$$self{outdir}/$$file{prefix}.chunks");
        }
        elsif ( $$file{pe} )
        {
            for my $chunk (@{$$file{chunks1}},@{$$file{chunks2}})
            {
                unlink("$chunk");
            }
            for my $chunk (@{$$file{chunks1}})
            {
                unlink("$chunk.sai1");
                unlink("$chunk.bam");
                unlink("$chunk.bam.bai");
            }
            for my $chunk (@{$$file{chunks2}})
            {
                unlink("$chunk.sai2");
            }
            unlink("$$self{outdir}/$$file{prefix}.chunks1");
            unlink("$$self{outdir}/$$file{prefix}.chunks2");
        }
        elsif ( $$file{bam_pe} )
        {
            for my $chunk (@{$$file{chunks}})
            {
                unlink("$chunk");
                unlink("$chunk.sai1");
                unlink("$chunk.sai2");
                unlink("$chunk.bam");
                unlink("$chunk.bam.bai");
            }
            unlink("$$self{outdir}/$$file{prefix}.chunks");
        }
        unlink("$$self{outdir}/bams/$$file{aln}.merge-list");
    }
}

sub list_se_files
{
    my ($self,$list) = @_;
    my @files = ();
    my $names = {};
    if ( !defined $list ) { return @files; }
    open(my $fh,'<',$list) or $self->throw("$list: $!");
    while (my $line=<$fh>)
    {
        my @items = split(/\s+/,$line);
        chomp($items[-1]);
        my $file = $items[0];
        my $aln  = $items[1];
        my $prefix;
        if ( $aln eq '-' )
        {
            if ( $items[2]=~/SM:([^\\]+)/ ) { $prefix = unique_name($names,$1); }
            elsif ( $file=~m{([^/]+)\.[^/]+$} ) { $prefix = unique_name($names,$1); }
            else { $prefix = unique_name($names,"xxx"); }
            $aln = "$prefix.bam";
        }
        elsif ( $aln=~m{\.[^\.]+$} )
        {
            $prefix = $`;
        }
        else { $self->throw("Could not parse file name: $aln"); }
        my $out = { se=>1, fastq=>$file, aln=>$aln, prefix=>$prefix, rg_line=>$items[2] };
        push @files, $out;
    }
    close($fh) or $self->throw("close failed: $list");
    return @files;
}
sub list_pe_files
{
    my ($self,$list) = @_;
    my @files = ();
    my $names = {};
    if ( !defined $list ) { return @files; }
    open(my $fh,'<',$list) or $self->throw("$list: $!");
    while (my $line=<$fh>)
    {
        my @items = split(/\s+/,$line);
        if ( @items!=3 ) { $self->throw("Expected: file1.fastq,file2.fastq  [out.bam|-]  \@RG\\tID:lane1\\tSM:60A_Sc_DBVPG6044"); }
        chomp($items[-1]);
        my ($file1,$file2) = split(/,/,$items[0]);
        if ( !defined $file2 ) { $self->throw("Expected two file names separated with a comma, got: $items[0]\n"); }
        my $aln = $items[1];
        my $prefix;
        if ( $aln eq '-' )
        {
            if ( $items[2]=~/SM:([^\\]+)/ ) { $prefix = unique_name($names,$1); }
            elsif ( $file1=~m{([^/]+)\.[^/]+$} ) { $prefix = unique_name($names,$1); }
            else { $prefix = unique_name($names,"xxx"); }
            $aln = "$prefix.bam";
        }
        elsif ( $aln=~m{\.[^\.]+$} )
        {
            $prefix = $`;
        }
        else { $self->throw("Could not parse file name: $aln"); }
        my $out = { pe=>1, fastq1=>$file1, fastq2=>$file2, aln=>$aln, prefix=>$prefix, rg_line=>$items[2] };
        push @files, $out;
    }
    close($fh) or $self->throw("close failed: $list");
    return @files;
}
sub unique_name
{
    my ($names,$name) = @_;
    my $idx = 0;
    my $tmp = $name;
    while (exists($$names{$tmp}))
    {
        $tmp = "$name.$idx";
        $idx++;
    }
    $$names{$tmp} = 1;
    return $tmp;
}
sub list_bam_files
{
    my ($self,$list) = @_;
    my @files = ();
    if ( !defined $list ) { return @files; }
    if ( !$self->is_finished("$$self{outdir}/input-bams.list") )
    {
        open(my $out,'>',"$$self{outdir}/input-bams.list.part") or $self->throw("$$self{outdir}/input-bams.list.part: $!");
        open(my $fh,'<',$list) or $self->throw("$list: $!");
        my $names = {};
        while (my $file=<$fh>)
        {
            if ( $file=~/^#/ ) { next; }
            $file =~ s/^\s*//;
            $file =~ s/\s*$//;
            my @rg   = ();
            my $smpl = undef;
            my ($fname,$hdr) = split(/\t/,$file);
            if ( defined $hdr && $hdr=~/^\@/ ) 
            {
                $file = $fname;
                @rg = split(/\\t/,$hdr);
                chomp($rg[-1]);
                for my $rg (@rg)
                {
                    if ( $rg=~/^SM:/ ) { $smpl = $'; }
                }
            }
            else
            {
                my @hdr  = $self->cmd("$$self{samtools} view -H $file");
                for my $line (@hdr)
                {
                    if ( !($line=~/^\@RG/) )  { next; }
                    for my $item (split(/\t/,$line))
                    { 
                        chomp($item);
                        if ( $item=~/^ID:/ ) { push @rg,$item; }
                        if ( $item=~/^PU:/ ) { push @rg,$item; }
                        if ( $item=~/^LB:/ ) { push @rg,$item; }
                        if ( $item=~/^SM:/ ) { push @rg,$item; $smpl = $'; }
                        if ( $item=~/^PL:/ ) { push @rg,$item; }
                        if ( $item=~/^CN:/ ) { push @rg,$item; }
                    }
                    last; 
                }
            }
            if ( !@rg ) { $self->throw("No \@RG line in $file?\n"); }
            if ( !$smpl ) { $self->throw("No \@RG SM field in $file?\n"); }
            my $new = unique_name($names,$smpl);
            print $out "$file\t$new.bam\t\@RG\\t".join("\\t",@rg)."\n";
        }
        close($fh) or $self->throw("close failed: $list");
        close($out) or $self->throw("close failed: $$self{outdir}/input-bams.list.part");
        rename("$$self{outdir}/input-bams.list.part","$$self{outdir}/input-bams.list") or $self->throw("rename $$self{outdir}/input-bams.list.part $$self{outdir}/input-bams.list: $!");
    }
    open(my $fh,'<',"$$self{outdir}/input-bams.list") or $self->throw("$$self{outdir}/input-bams.list: $!");
    while (my $line=<$fh>)
    {
        my ($ori,$new,$rg_line) = split(/\t/,$line);
        chomp($rg_line);
        if ( !($new=~m{\.[^\.]+$} ) ) { $self->throw("Could not parse file name: $new"); }
        my $prefix = $`;
        my $out = { bam_pe=>1, bam=>$ori, aln=>$new, prefix=>$prefix, rg_line=>$rg_line };
        push @files, $out;
    }
    close($fh) or $self->throw("close failed: $$self{outdir}/input-bams.list");
    return @files;
}
sub list_chunks
{
    my ($self,$file) = @_;
    my @chunks = ();
    open(my $fh,'<',$file) or $self->throw("$file: $!");
    while (my $line=<$fh>)
    {
        $line =~ s/\s*$//;
        $line =~ s/^\s*//;
        chomp($line);
        push @chunks,$line;
    }
    close($fh) or $self->throw("close failed: $file");
    return \@chunks;
}

sub split_fastq
{
    my ($self,$outfile,$infile) = @_;
    open(my $list,'>',"$outfile.part") or $self->throw("$outfile.part: $!");
    my ($in,$cmd);
    if ( $infile=~/\.gz$/ )
    {
        $cmd = "gunzip -c $infile";
        open($in,"$cmd |") or $self->throw("$cmd: $!");
    }
    else
    {
        $cmd = $infile;
        open($in,'<',$cmd) or $self->throw("$cmd: $!");
    }
    my $out    = undef;
    my $nout   = 0;
    my $ichunk = 0;
    while (my $line=<$in>)
    {
        if ( $nout >= $$self{reads_per_chunk}*4 )
        {
            close($out) or $self->throw("close failed: $outfile.$ichunk");
            $out = undef;
        }
        if ( !defined $out )
        {
            $nout = 0;
            $ichunk++;
            if ( $$self{debug_chunks} && $ichunk > $$self{debug_chunks} ) { last; }
            open($out,"| gzip -c > $outfile.$ichunk") or $self->throw("gzip -c > $outfile.$ichunk: $!");
            print $list "$outfile.$ichunk\n";
        }
        print $out $line;
        $nout++;
    }
    if ( defined $out )
    {
        close($out) or $self->throw("close failed: $outfile.$ichunk");
    }
    close($in) or ($$self{debug_chunks} || $self->throw("close failed: $cmd"));
    close($list) or $self->throw("close failed: $outfile.part");
    rename("$outfile.part",$outfile) or $self->throw("rename $outfile.part $outfile: $!");
}
sub split_bam2fq
{
    my ($self,$outfile,$infile) = @_;
    if ( $$self{bam_sort} )
    {
        if ( !$self->is_finished("$outfile.sort.bam") )
        {
            my $cmd = $$self{bam_sort};
            $cmd =~ s/{IN}/$infile/;
            $cmd =~ s/{OUT}/$outfile.sort.part/;
            $self->cmd($cmd);
            rename("$outfile.sort.part.bam","$outfile.sort.bam") or $self->throw("rename $outfile.sort.part.bam $outfile.sort.bam: $!");
        }
        $infile = "$outfile.sort.bam";
    }
    open(my $list,'>',"$outfile.part") or $self->throw("$outfile.part: $!");
    open(my $in,"$$self{samtools} view $infile |") or $self->throw("$$self{samtools} view $infile failed: $!");
    my $out1   = undef;
    my $out2   = undef;
    my $nout   = 0;
    my $ichunk = 0;
    my @buf    = ();
    my $ntot   = 0;
    my $nwr    = 0;
    my $bam_tag = $$self{bam_tag} ? "$$self{bam_tag}:" : 0;
    while (my $line=<$in>)
    {
        $ntot++;
        my @vals = split(/\t/,$line);
        my $name = $vals[0];
        my $flag = $vals[1];
        my $seq  = $vals[9];
        my $qual = $vals[10];
        my $tag  = undef;
        if ( $bam_tag )
        {
            for (my $i=11; $i<@vals; $i++)
            {
                if ( substr($vals[$i],0,3) eq $bam_tag ) { $tag = $vals[$i]; last; }
            }
            if ( !defined $tag ) { next; }
        }
        push @buf, { name=>$name, seq=>$seq, qual=>$qual, tag=>$tag, flag=>$flag };

        if ( @buf==1 ) { next; }
        if ( $buf[0]{name} ne $buf[1]{name} ) { shift @buf; next; }

        if ( $nout >= $$self{reads_per_chunk} )
        {
            close($out1) or $self->throw("close failed: $outfile.1.$ichunk");
            close($out2) or $self->throw("close failed: $outfile.2.$ichunk");
            $out1 = undef;
            $out2 = undef;
        }
        if ( !defined $out1 )
        {
            $nout = 0;
            $ichunk++;
            if ( $$self{debug_chunks} && $ichunk > $$self{debug_chunks} ) { last; }
            open($out1,"| gzip -c > $outfile.1.$ichunk") or $self->throw("gzip -c > $outfile.1.$ichunk: $!");
            open($out2,"| gzip -c > $outfile.2.$ichunk") or $self->throw("gzip -c > $outfile.2.$ichunk: $!");
            print $list "$outfile.1.$ichunk\t$outfile.2.$ichunk\n";
        }
        if ( $buf[0]{flag} & 0x80 )
        {
            my $tmp = $buf[0]; $buf[0] = $buf[1]; $buf[1] = $tmp;
        }
        print $out1 '@'.$buf[0]{name}."\t".$buf[0]{tag}."\n".$buf[0]{seq}."\n+\n".$buf[0]{qual}."\n";
        print $out2 '@'.$buf[1]{name}."\t".$buf[1]{tag}."\n".$buf[1]{seq}."\n+\n".$buf[1]{qual}."\n";
        splice(@buf,0,2);
        $nout++;
        $nwr+=2;
    }
    if ( defined $out1 )
    {
        close($out1) or $self->throw("close failed: $outfile.1.$ichunk");
        close($out2) or $self->throw("close failed: $outfile.2.$ichunk");
    }
    close($in) or ($$self{debug_chunks} || $self->throw("close failed: gunzip -c $infile failed"));
    close($list) or $self->throw("close failed: $outfile.part");
    open(my $fh,'>',"$$self{outdir}/split-bam2fq-stats.txt") or $self->throw("$$self{outdir}/split-bam2fq-stats.txt: $!");
    print $fh "reads_total\t$ntot\n";
    print $fh "reads_written\t$nwr\n";
    close($fh) or $self->throw("close failed: $$self{outdir}/split-bam2fq-stats.txt");
    rename("$outfile.part",$outfile) or $self->throw("rename $outfile.part $outfile: $!");
}
sub split_bam
{
    my ($self,$outfile,$infile) = @_;
    if ( $$self{bam_sort} )
    {
        if ( !$self->is_finished("$outfile.sort.bam") )
        {
            my $cmd = $$self{bam_sort};
            $cmd =~ s/{IN}/$infile/;
            $cmd =~ s/{OUT}/$outfile.sort.part/;
            $self->cmd($cmd);
            rename("$outfile.sort.part.bam","$outfile.sort.bam") or $self->throw("rename $outfile.sort.part.bam $outfile.sort.bam: $!");
        }
        $infile = "$outfile.sort.bam";
    }
    open(my $list,'>',"$outfile.part") or $self->throw("$outfile.part: $!");
    open(my $in,"$$self{samtools} view -h $infile |") or $self->throw("$$self{samtools} view -h $infile failed: $!");
    my $out    = undef;
    my $nout   = 0;
    my $ichunk = 0;
    my @hdr    = ();
    my $prev   = undef;
    my $iline  = 0;
    while (my $line=<$in>)
    {
        $iline++;
        if ( $line=~/^\@/ ) { push @hdr,$line; next; }
        if ( $nout >= $$self{reads_per_chunk}*2 )   # assuming paired-end
        {
            close($out) or $self->throw("close failed: $outfile.$ichunk");
            $out = undef;
        }
        if ( !defined $out )
        {
            $nout = 0;
            $ichunk++;
            if ( $$self{debug_chunks} && $ichunk > $$self{debug_chunks} ) { last; }
            open($out,"| $$self{samtools} view -b -o $outfile.$ichunk") or $self->throw("$$self{samtools} view -b -o $outfile.$ichunk: $!");
            print $out join('',@hdr);
            print $list "$outfile.$ichunk\n";
        }
        my $i = index($line,"\t");
        my $name = substr($line,0,$i);
        if ( !defined $prev ) { $prev = $name; }
        elsif ( $prev ne $name ) { $self->throw("Non-matching pair names at line $iline: $prev vs $name\n"); }
        else { $prev = undef; }
        print $out $line;
        $nout++;
    }
    if ( defined $out )
    {
        close($out) or $self->throw("close failed: $outfile.$ichunk");
    }
    close($in) or ($$self{debug_chunks} || $self->throw("close failed: $$self{samtools} view -h $infile failed"));
    close($list) or $self->throw("close failed: $outfile.part");
    unlink("$outfile.sort.bam");
    rename("$outfile.part",$outfile) or $self->throw("rename $outfile.part $outfile: $!");
}

sub bwa_aln
{
    my ($self,$sai,$fastq,$args) = @_;

    my @lines = $self->cmd("zcat $fastq | wc -l");
    my $nrec = $lines[0];
    chomp($nrec);
    if ( $nrec % 4 ) { $self->throw("Broken fastq, $nrec lines: $fastq\n"); }
    $nrec /= 4;

    my @out = $self->cmd("$$self{bwa} aln $args $$self{fa_ref} $fastq  2>&1 > $sai.part");
    my $nproc = 0;
    for my $line (@out)
    {
        if ( $line=~/^\[bwa_aln_core\]\s+(\d+) sequences have been processed/ && $nproc < $1 ) { $nproc = $1; }
    }
    if ( $nrec != $nproc )
    {
        $self->throw("Wrong number of records in fastq vs bwa aln output:\n\t$nrec .. $fastq\n\t".join("\n\t",@out)."\n\n"); 
    }

    rename("$sai.part",$sai) or $self->throw("rename $sai.part $sai: $!");
}
sub bwa_aln_bam
{
    my ($self,$sai1,$sai2,$bam,$args) = @_;

    my @lines = $self->cmd("$$self{samtools} stats $bam | grep ^SN | grep 'raw total sequences'");
    my (undef,undef,$nrec) = split(/\t/,$lines[0]);
    chomp($nrec);
    if ( $nrec % 2 ) { $self->throw("Broken paired bam, $nrec lines: $bam\n"); }
    $nrec /= 2;

    my @out = $self->cmd("$$self{bwa} aln $args $$self{fa_ref} -b1 $bam 2>&1 > $sai1.part");
    my $nproc = 0;
    for my $line (@out)
    {
        if ( $line=~/^\[bwa_aln_core\]\s+(\d+) sequences have been processed/ && $nproc < $1 ) { $nproc = $1; }
    }
    if ( $nrec != $nproc )
    {
        $self->throw("Wrong number of records in fastq vs bwa aln output:\n\t$nrec .. $bam\n\t".join("\n\t",@out)."\n\n"); 
    }

    @out = $self->cmd("$$self{bwa} aln $args $$self{fa_ref} -b2 $bam 2>&1 > $sai2.part");
    $nproc = 0;
    for my $line (@out)
    {
        if ( $line=~/^\[bwa_aln_core\]\s+(\d+) sequences have been processed/ && $nproc < $1 ) { $nproc = $1; }
    }
    if ( $nrec != $nproc )
    {
        $self->throw("Wrong number of records in fastq vs bwa aln output:\n\t$nrec .. $bam\n\t".join("\n\t",@out)."\n\n"); 
    }

    rename("$sai2.part",$sai2) or $self->throw("rename $sai2.part $sai2: $!");
    rename("$sai1.part",$sai1) or $self->throw("rename $sai1.part $sai1: $!");
}

sub bwa_samse
{
    my ($self,$bam,%args) = @_;
    my $fastq   = $args{fastq};
    my $sai     = $args{sai};
    my $rg_line = $args{rg};
    my $filter  = $$self{filter} ? $$self{filter} : '';
    if ( $filter )
    {
        $filter  =~ s/^\s*\|//;
        $filter  =~ s/\|\s*$//;
        $filter .= ' |';
    }
    $self->cmd(qq[$$self{bwa} samse $$self{fa_ref} -r '$rg_line' $sai $fastq | $filter $$self{samtools} sort -o $bam.part]);
    $self->cmd("$$self{samtools} index $bam.part");
    rename("$bam.part.bai","$bam.bai") or $self->throw("rename $bam.part.bai $bam.bai: $!");
    rename("$bam.part",$bam) or $self->throw("rename $bam.part $bam: $!");
}
sub bwa_mem
{
    my ($self,$bam,%args) = @_;
    my $fastqs   = $args{fastqs};   # one or two fastq fils
    my $rg_line  = $args{rg};
    my $filter   = $$self{filter} ? $$self{filter} : '';
    if ( $filter )
    {
        $filter  =~ s/^\s*\|//;
        $filter  =~ s/\|\s*$//;
        $filter .= ' |';
    }
    my $args = $$self{bam_tag} ? '-C' : '';
    if ( $$self{aln_args} ne '' ) { $args .= " $$self{aln_args}"; }
    $self->cmd("mkdir -p $bam.tmpdir");
    $self->cmd(qq[$$self{bwa} mem $args $$self{fa_ref} -R '$rg_line' $fastqs | $filter $$self{samtools} sort -o $bam.part -T $bam.tmpdir/samtools.sort]);
    $self->cmd("rmdir $bam.tmpdir");

    $self->cmd("$$self{samtools} index $bam.part");
    rename("$bam.part.bai","$bam.bai") or $self->throw("rename $bam.part.bai $bam.bai: $!");
    rename("$bam.part",$bam) or $self->throw("rename $bam.part $bam: $!");
}
sub bwa_sampe
{
    my ($self,$bam,%args) = @_;
    my $fastq1   = $args{fastq1};
    my $fastq2   = $args{fastq2};
    my $sai1     = $args{sai1};
    my $sai2     = $args{sai2};
    my $rg_line  = $args{rg};
    my $filter   = $$self{filter} ? $$self{filter} : '';
    if ( $filter )
    {
        $filter  =~ s/^\s*\|//;
        $filter  =~ s/\|\s*$//;
        $filter .= ' |';
    }
    $self->cmd(qq[$$self{bwa} sampe $$self{fa_ref} -r '$rg_line' $sai1 $sai2 $fastq1 $fastq2 | $filter $$self{samtools} sort -o $bam.part]);
    $self->cmd("$$self{samtools} index $bam.part");
    rename("$bam.part.bai","$bam.bai") or $self->throw("rename $bam.part.bai $bam.bai: $!");
    rename("$bam.part",$bam) or $self->throw("rename $bam.part $bam: $!");
}

sub merge
{
    my ($self,$bam,$chunks) = @_;
    open(my $fh,'>',"$bam.merge-list") or $self->throw("$bam.merge-list");
    for my $chunk (@$chunks)
    {
        print $fh "$chunk.bam\n";
    }
    close($fh) or $self->throw("close failed: $bam.merge-list");
    $self->cmd(qq[$$self{samtools} merge -fc -b $bam.merge-list $bam.part]);
    $self->cmd("$$self{samtools} index $bam.part");
    $self->cmd("$$self{samtools} stats -r $$self{fa_ref} $bam.part > $bam.bchk");
    rename("$bam.part.bai","$bam.bai") or $self->throw("rename $bam.part.bai $bam.bai: $!");
    rename("$bam.part",$bam) or $self->throw("rename $bam.part $bam: $!");
}

sub rmdup
{
    my ($self,$outfile,$bam) = @_;
    $self->cmd("$$self{picard} I=$bam O=$outfile.part M=$outfile.txt",java_err_file=>"$outfile.java");
    $self->cmd("$$self{samtools} index $outfile.part");
    rename("$outfile.part.bai","$outfile.bai") or $self->throw("rename $outfile.part.bai $outfile.bai: $!");
    rename("$outfile.part",$outfile) or $self->throw("rename $outfile.part $outfile: $!");
}


